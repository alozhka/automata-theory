# Грамматика выражений языка MysticGameScript

## Синтаксис выражений

Выражения могут содержать:

- Литералы чисел
- Литералы строк
- Идентификаторы
- Логические константы
- Операторы (бинарные и унарные)
- Лексемы круглых скобок (дают приоритет)

## Операторы

Арифметические операторы:

| Символы | Операция                                          |
| ------- | ------------------------------------------------  |
| `+`     | Сложение или унарный "+" (для строк конкатенация) |
| `-`     | Вычитание или унарный "-"                         |
| `*`     | умножение                                         |
| `/`     | деление                                           |
| `//`    | целочисленное деление                             |
| `%`     | остаток от деления                                |

Логические операторы:

| Символы | Операция              |
| ------- | --------------------- |
| &&      | логическое И          |
| ||      | логическое ИЛИ        |
| !       | логическое НЕ         |

Операторы сравнения:

| Символы | Операция              |
| ------- | --------------------- |
| !=      | неравенство           |
| ==      | равенство             |
| <=      | меньше или равно      |
| >=      | больше или равно      |
| <       | меньше                |
| >       | больше                |

Оператор присваивания:

| Символы | Операция              |
| ------- | --------------------- |
| =       | присваивание          |

## Приоритет операторов

| Приоритет (по убыванию) | Операторы            |
| ----------------------- | -------------        |
| 8                       | Унарные `-`, `!`     |
| 7                       | `*`, `/`, `//`, `%`  |
| 6                       | `+`, `-`             |
| 5                       | `>`, `>=`, `<`, `<=` |
| 4                       | `==`, `!=`           |
| 3                       | `&&`                 |
| 2                       | `\|\|`               |
| 1                       | `=`                  |

## Грамматика в нотации EBNF

* цифра = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

* буква = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" 
       | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" 
       | "u" | "v" | "w" | "x" | "y" | "z" 
       | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" 
       | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" 
       | "U" | "V" | "W" | "X" | "Y" | "Z" ;

* спец_символ = "_";

* символ = любой символ Unicode

* идентификатор = буква, {буква | цифра | спец_символ}
              | спец_символ, буква, {буква | цифра | спец_символ};

* целое_число = цифра, {цифра};
* вещественное_число = целое_число, ".", целое_число;
* число = целое_число | вещественное_число;
* строка = '"', {символ}, '"' | "'", {символ}, "'";

### Типы данных
* базовый_тип = "dayzint" | "fallout" | "strike" | "statum";
* тип_массива = "araya", "<", тип, ">";
* нулл_тип = "?", базовый_тип | "?", тип_массива;
* тип = базовый_тип | тип_массива | нулл_тип;

### Операторы
* оператор_сравнения = "<" | ">" | "==" | "!=" | "<=" | ">=";
* унарный_оператор = "!" | "-";
* мультипликативный_оператор = "*" | "/" | "//" | "%";
* адитивный_оператор = "+" | "-";
* логический_оператор = "&&" | "||";

### Выражения
* литерал_выражения = число | строка | "ready" | "noready" | "ghost";
* часть_выражения = литерал_выражения | идентификатор 
              | вызов_функции | вызов_заложенной_функции
              | элемент_массива | "(", выражение, ")";
* унарное_выражение = [унарный_оператор], часть_выражения;
* мультипликативное_выражение = унарное_выражение, {мультипликативный_оператор, унарное_выражение};
* аддитивное_выражение = мультипликативное_выражение, {адитивный_оператор, мультипликативное_выражение};
* выражение_сравнения = аддитивное_выражение, [оператор_сравнения, аддитивное_выражение];
* логическое_выражение = выражение_сравнения, {логический_оператор, выражение_сравнения};
* выражение = логическое_выражение;

### ...
* элемент_массива = идентификатор, "[", выражение, "]";
* присваивание = идентификатор, "=", выражение;

### Объявления
* объявление_переменной = ["?"], тип, идентификатор, ["=", выражение];
* объявление_константы = "monument", тип, присваивание;

### Управляющие конструкции
* если = "iffy", "(", выражение, ")", блок;
* иначе_если = "elysiffy", "(", выражение, ")", блок;
* иначе = "elysian", блок;
* условие = если, {иначе_если}, [иначе];

* прерывание_цикла = "breakout", ";" | "contra", ";";
* цикл_while = "valorant", "(", выражение, ")", блок;
* цикл_for = "forza", "(", (объявление_переменной | присваивание), ";", выражение, ";", присваивание, ")", блок;

* инструкция = объявление_переменной | объявление_константы | присваивание | условие 
              | цикл_while | цикл_for | вызов_функции | вызов_заложенной_функции
              | прерывание_цикла | возврат, ";";
* блок = "{", {инструкция}, "}";

### Функции
* параметр = тип, идентификатор;
* список_параметров = параметр, {",", параметр};
* возврат = "returnal", [выражение], ";";
* объявление_функции = "funkotron", идентификатор, "(", [список_параметров], ")", [":", тип], блок;
* вызов_функции = идентификатор, "(", [выражение, {",", выражение}], ")";

* вызов_заложенной_функции = "min", "(", выражение, ",", выражение, {",", выражение}, ")" 
                     | "max", "(", выражение, ",", выражение, {",", выражение}, ")"
                     | "abs", "(", выражение ")"
                     | "round", "(", выражение ")"
                     | "ceil", "(", выражение ")"
                     | "floor", "(", выражение ")"
                     | "exodus", "(", выражение, ")"
                     | "exodusln", "(", выражение, ")"
                     | "raid", "(", идентификатор, ")";

### Комментарии

* однострочный_комментарий = "#", {символ};
* многострочный_комментарий = "\\\*", {символ}, "\*\\"